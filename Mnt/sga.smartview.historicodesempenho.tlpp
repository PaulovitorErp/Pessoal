#include "msobject.ch"
#include "tlpp-core.th"
#include "tlpp-rest.th"
#include "totvs.framework.treports.integratedprovider.th"

namespace totvs.protheus.SGA.HistoricoDesempenhoSmartViewBusinessObject

//-------------------------------------------------------------------
/*/{Protheus.doc} HistoricoDesempenhoSmartViewBusinessObject
Objeto de negócio histórico desempenho
Tabelas: TAA, TAB, TAD, TAE, TAF, TAO, TA4, TA6, TA7, TA8, TBB
@type Classe

@author Eloisa Anibaletto
@since 24/04/2024
@version P11
@return Nil
/*/
//-------------------------------------------------------------------
@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGASGA", tables="TAA, TAB, TAD, TAE, TAF, TAO, TA4, TA6, TA7, TA8, TBB", name="Histórico Desempenho", country="ALL", initialRelease="12.1.2310")
class HistoricoDesempenhoSmartViewBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider

	public method new() as object
	public method getAreas() as array
	public method getDescription() as character
	public method getData() as object
	public method getSchema() as object

endclass

//-------------------------------------------------------------------
/*/{Protheus.doc} New
Método Construtor do Objeto de Negócio
@type Método

@author Eloisa Anibaletto
@since 24/04/2024
@version P11
@return Nil
/*/
//-------------------------------------------------------------------
method new() as object class HistoricoDesempenhoSmartViewBusinessObject
	_Super:new()
	self:setDisplayName( "Histórico Desempenho" )
return self

//-------------------------------------------------------------------
/*/{Protheus.doc} getDescription
Descrição do Objeto de Negócio
@type MétodoS

@author Eloisa Anibaletto
@since 24/04/2024
@version P11
@return caractere, Conteúdo da descrição do Objeto de Negócios
/*/
//-------------------------------------------------------------------
method getDescription() as character class HistoricoDesempenhoSmartViewBusinessObject
return "Relatório Histórico Desempenho"

//-------------------------------------------------------------------
/*/{Protheus.doc} getAreas
Áreas do menu que o Objeto de Negócio deve ficar
@type Método

@author Eloisa Anibaletto
@since 24/04/2024
@version P11
@return array, Retorna um array contendo as sessões do Menu em que o Objeto de Negócio deve aparecer
/*/
//-------------------------------------------------------------------
method getAreas() as array class HistoricoDesempenhoSmartViewBusinessObject
return { "SGA" }

//-------------------------------------------------------------------
/*/{Protheus.doc} GetData
Método responsável pelo processamento dos dados enviados pelo 
Objeto de Negócio
@type Método

@author Eloisa Anibaletto
@since 24/04/2024
@version P11

@param nPage, numerico, Número da página atual
@param oFilter, objeto, Filtros aplicados pelo usuário

@return objeto, Retorna o próprio objeto
/*/
//-------------------------------------------------------------------
method getData( nPage as numeric, oFilter as object ) as object class HistoricoDesempenhoSmartViewBusinessObject

	Local jParams     as json
	Local ojItems     as json

	Local aTAD1       as array
	Local aTAD2       as array

	Local cAliasTAB   as character
	Local cAliasTAD   as character
	Local cAva1       as character
	Local cAva2       as character
	Local cOpc1       as character
	Local cOpc2       as character
	Local cTAA        as character
	Local cTAB        as character
	Local cTAD        as character
	Local cTAE        as character
	Local cTAF        as character
	Local cTAO        as character
	Local cTA4        as character
	Local cTA6        as character
	Local cTA7        as character
	Local cTA8        as character
	Local cTBB        as character

	Local nCount      as numeric
	Local nTAD1       as numeric
	Local nTAD2       as numeric

	aTAD1      := {}
	aTAD2      := {}
	nCount     := 0
	nTAD1      := 0
	nTAD2      := 0
	jParams    := oFilter:getParameters() // metodo para retorno do json dos parâmetros

	cAliasTAB  := GetNextAlias()
	cAliasTAD  := GetNextAlias()
	cTAA       := xFilial( "TAA" )
	cTAB       := xFilial( "TAB" )
	cTAD       := xFilial( "TAD" )
	cTAE       := xFilial( "TAE" )
	cTAF       := xFilial( "TAF" )
	cTAO       := xFilial( "TAO" )
	cTA4       := xFilial( "TA4" )
	cTA6       := xFilial( "TA6" )
	cTA7       := xFilial( "TA7" )
	cTA8       := xFilial( "TA8" )
	cTBB       := xFilial( "TBB" )

	BeginSQL Alias cAliasTAB
		SELECT TAB.TAB_ORDEM, TAB.TAB_DTRESU, TAB.TAB_CODNIV, TAB.TAB_CODPLA, TAB.TAB_CODEME, TAB.TAB_REVISA,
				TAB.TAB_CODASP, TAB.TAB_CODIMP, TAB.TAB_CODCLA, TAO.TAO_CODHIS, TAF.TAF_NOMNIV, TAA.TAA_NOME, 
				TBB.TBB_DESPLA, TBB.TBB_CODPLA, TA4.TA4_DESCRI, TAE.TAE_DESCRI, TAE.TAE_CODIMP, TA8.TA8_DESCRI
		FROM %table:TAB% TAB
		LEFT JOIN %table:TAO% TAO ON
			TAO.TAO_ORDEM = TAB.TAB_ORDEM
			AND %exp:cTAO% = TAO.TAO_FILIAL 
			AND TAO.%notDel%
		LEFT JOIN %table:TAF% TAF ON
			TAF.TAF_CODNIV = TAB.TAB_CODNIV
			AND %exp:cTAF% = TAF.TAF_FILIAL
			AND TAF.%notDel%
		LEFT JOIN %table:TAA% TAA ON
			TAA.TAA_CODPLA = TAB.TAB_CODPLA
			AND %exp:cTAA% = TAA.TAA_FILIAL
			AND TAA.%notDel%
		LEFT JOIN %table:TBB% TBB ON
			TBB.TBB_CODPLA = TAB.TAB_CODEME
			AND %exp:cTBB% = TBB.TBB_FILIAL
			AND TBB.%notDel%
		LEFT JOIN %table:TA4% TA4 ON
			TA4.TA4_CODASP = TAB.TAB_CODASP
			AND %exp:cTA4% = TA4.TA4_FILIAL
			AND TA4.%notDel%
		LEFT JOIN %table:TA8% TA8 ON
			TA8.TA8_CODCLA = TAB.TAB_CODCLA
			AND %exp:cTA8% = TA8.TA8_FILIAL
			AND TA8.%notDel%
		LEFT JOIN %table:TAE% TAE ON
			TAE.TAE_CODIMP = TAB.TAB_CODIMP
			AND %exp:cTAE% = TAE.TAE_FILIAL
			AND TAE.%notDel%
      	WHERE 
			TAB.TAB_FILIAL = %exp:cTAB%
			AND TAB.TAB_CODASP BETWEEN %exp:jParams[ "MV_PAR01", 1 ]% AND %exp:jParams[ "MV_PAR02", 1 ]%
			AND TAB.TAB_CODIMP BETWEEN %exp:jParams[ "MV_PAR03", 1 ]% AND %exp:jParams[ "MV_PAR04", 1 ]%
			AND TAB.TAB_CODNIV BETWEEN %exp:jParams[ "MV_PAR05", 1 ]% AND %exp:jParams[ "MV_PAR06", 1 ]%
			AND TAB.TAB_DTRESU BETWEEN %exp:jParams[ "MV_PAR07", 1 ]% AND %exp:jParams[ "MV_PAR08", 1 ]%
			AND TAB.TAB_REVISA = %exp:jParams[ "Mv_Par11", 1 ]% 
			AND TAB.%notDel%
	EndSQL

	dbSelectArea( cAliasTAB )
	dbGoTop()

	BeginSQL Alias cAliasTAD
		SELECT TAD.TAD_CODAVA, TAD.TAD_CODOPC, TAD.TAD_INDICA, TAD.TAD_ORDEM
		FROM %table:TAD% TAD
	WHERE 
		TAD.TAD_FILIAL = %exp:cTAD%
		AND TAD.TAD_ORDEM IN (SELECT DISTINCT %exp:( cAliasTAB )->TAB_ORDEM% FROM %table:cAliasTAB% )
		AND TAD.%notDel%
	EndSQL

	dbSelectArea( cAliasTAD )
	dbGoTop()

	While !( cAliasTAD )->( Eof() )

		If ( cAliasTAD )->TAD_INDICA == "1"
			cAva1 := ( cAliasTAD )->TAD_CODAVA + Space( 3 ) + SubStr( Posicione( "TA6", 1, xFilial( "TAD" ) + ( cAliasTAD )->TAD_CODAVA, "TA6_DESCRI" ), 1, 30 ) 
			cOpc1 := ( cAliasTAD )->TAD_CODOPC + Space( 3 ) + Posicione( "TA7", 1, xFilial( "TA7" ) + ( cAliasTAD )->TAD_CODAVA + ( cAliasTAD )->TAD_CODOPC, "TA7_OPCAO" ) 
			aAdd( aTAD1, { cAva1, cOpc1 } )
		ElseIf ( cAliasTAD )->TAD_INDICA == "2"
			cAva2 := ( cAliasTAD )->TAD_CODAVA + Space( 3 ) + SubStr( Posicione( "TA6", 1, xFilial( "TAD" ) + ( cAliasTAD )->TAD_CODAVA, "TA6_DESCRI" ), 1, 30 ) 
			cOpc2 := ( cAliasTAD )->TAD_CODOPC + Space( 3 ) + Posicione( "TA7", 1, xFilial( "TA7" ) + ( cAliasTAD )->TAD_CODAVA + ( cAliasTAD )->TAD_CODOPC, "TA7_OPCAO" )
			aAdd( aTAD2, { cAva2, cOpc2 } )
		EndIf

		( cAliasTAD )->( dbSkip() )

	End

	self:setPageSize( 15 )

	While !( cAliasTAB )->( Eof() )

		nTAD1 := Len( aTAD1 ) 
		nTAD2 := Len( aTAD2 ) 

		ojItems := JsonObject():new()
		ojItems[ "TAB_ORDEM"   ] := ( cAliasTAB )->TAB_ORDEM
		ojItems[ "TAO_CODHIS"  ] := ( cAliasTAB )->TAO_CODHIS
		ojItems[ "TAB_DTRESU"  ] := totvs.framework.treports.date.stringToTimeStamp( ( cAliasTAB )->TAB_DTRESU )
		ojItems[ "TAB_CODNIV"  ] := ( cAliasTAB )->TAB_CODNIV
		ojItems[ "TAF_NOMNIV"  ] := ( cAliasTAB )->TAF_NOMNIV
		ojItems[ "TAB_CODPLA"  ] := ( cAliasTAB )->TAB_CODPLA
		ojItems[ "TAA_NOME"    ] := ( cAliasTAB )->TAA_NOME
		ojItems[ "TAB_CODEME"  ] := ( cAliasTAB )->TAB_CODEME
		ojItems[ "TBB_DESPLA"  ] := ( cAliasTAB )->TBB_DESPLA
		ojItems[ "TAB_CODASP"  ] := ( cAliasTAB )->TAB_CODASP
		ojItems[ "TA4_DESCRI"  ] := ( cAliasTAB )->TA4_DESCRI
		ojItems[ "TAB_CODIMP"  ] := ( cAliasTAB )->TAB_CODIMP
		ojItems[ "TAE_DESCRI"  ] := ( cAliasTAB )->TAE_DESCRI
		ojItems[ "TAB_CODCLA"  ] := ( cAliasTAB )->TAB_CODCLA
		ojItems[ "TA8_DESCRI"  ] := ( cAliasTAB )->TA8_DESCRI
		ojItems[ "TAD_CODAVA1" ] := aTAD1[ nTAD1, 1 ]
		ojItems[ "TAD_CODOPC1" ] := aTAD1[ nTAD1, 2 ]
		ojItems[ "TAD_CODAVA2" ] := aTAD2[ nTAD2, 1 ]
		ojItems[ "TAD_CODOPC2" ] := aTAD2[ nTAD2, 2 ]

		self:oData:appendData( ojItems )

		( cAliasTAB )->( dbSkip() )

		nCount++

	Enddo

	self:setHasNext( ( cAliasTAD )->( !Eof() ) )
	self:setHasNext( ( cAliasTAB )->( !Eof() ) )

	( cAliasTAD )->( DBCloseArea() )
	( cAliasTAB )->( DBCloseArea() )

return self:oData

//-------------------------------------------------------------------
/*/{Protheus.doc} getSchema
Método responsável por informar as características dos campos e 
também os parâmetros quando esses não são adicionados pela SX1
@type Método

@author Eloisa Anibaletto
@since 24/04/2024
@version P11

@return objeto, Retorna a si mesmo
/*/
//-------------------------------------------------------------------
method getSchema() as object class HistoricoDesempenhoSmartViewBusinessObject

	self:AddParameter( "MV_PAR01", "De Aspecto" , "number", .F. )
	self:AddParameter( "MV_PAR02", "Ate Aspecto", "number", .F. )
	self:AddParameter( "MV_PAR03", "De Impacto" , "number", .F. )
	self:AddParameter( "MV_PAR04", "Ate Impacto", "number", .F. )
	self:AddParameter( "MV_PAR05", "De Nível"   , "number", .F. )
	self:AddParameter( "MV_PAR06", "Ate Nível"  , "number", .F. )
	self:AddParameter( "MV_PAR07", "De Data"    , "date"  , .F. )
    self:AddParameter( "MV_PAR08", "Ate Data"   , "date"  , .F. )
	self:AddParameter( "MV_PAR11", "Revisão"    , "number", .F. )

	//Combos e Lookups
	self:setCustomURL( "MV_PAR01", "api/framework/v1/genericLookupService/smartview/TA4", 2 )
	self:setCustomURL( "MV_PAR02", "api/framework/v1/genericLookupService/smartview/TA4", 2 )
	self:setCustomURL( "MV_PAR03", "api/framework/v1/genericLookupService/smartview/TAE", 2 )
	self:setCustomURL( "MV_PAR04", "api/framework/v1/genericLookupService/smartview/TAE", 2 )
	self:setCustomURL( "MV_PAR05", "api/framework/v1/genericLookupService/smartview/TAF", 2 )
	self:setCustomURL( "MV_PAR06", "api/framework/v1/genericLookupService/smartview/TAF", 2 )
	self:setCustomURL( "MV_PAR11", "api/framework/v1/genericLookupService/smartview/TDR", 2 )

	self:addProperty( "TAB_ORDEM"  , "Número da Ordem"               , "string", "Ordem"         , "TAB_ORDEM"  )
	self:addProperty( "TAO_CODHIS" , "Código do Histórico"           , "string", "Histórico"     , "TAO_CODHIS" )
	self:addProperty( "TAB_DTRESU" , "Data do Resultado"             , "date"  , "Data Resultado", "TAB_DTRESU" )
	self:addProperty( "TAB_CODNIV" , "Código do Nível"               , "string", "Nível"         , "TAB_CODNIV" )
	self:addProperty( "TAF_NOMNIV" , "Nome do Nível"                 , "string", "Nome Nível"    , "TAF_NOMNIV" )
	self:addProperty( "TAB_CODPLA" , "Código do Plano de Ação"       , "string", "Cod. Plan."    , "TAB_CODPLA" )
	self:addProperty( "TAA_NOME"   , "Nome do Plano de Ação"         , "string", "Nome Plano"    , "TAA_NOME"   )
	self:addProperty( "TAB_CODEME" , "Código do Plano Emergencial"   , "string", "Plano Emerg."  , "TAB_CODEME" )
	self:addProperty( "TBB_DESPLA" , "Descrição do Plano Emergencial", "string", "Descrição"     , "TBB_DESPLA" )
	self:addProperty( "TAB_CODASP" , "Código do Aspecto"             , "string", "Aspecto"       , "TAB_CODASP" )
	self:addProperty( "TA4_DESCRI" , "Descrição do Aspecto"          , "string", "Desc. Ascpecto", "TA4_DESCRI" )
	self:addProperty( "TAD_CODAVA1", "Código da Avaliação"           , "string", "Avaliação 1"   , "TAD_CODAVA1")
	self:addProperty( "TAD_CODOPC1", "Código da Opção"               , "string", "Opção 1"       , "TAD_CODOPC1")
	self:addProperty( "TAD_CODAVA2", "Código da Avaliação"           , "string", "Avaliação 2"   , "TAD_CODAVA2")
	self:addProperty( "TAD_CODOPC2", "Código da Opção"               , "string", "Opção 2"       , "TAD_CODOPC2")
	self:addProperty( "TAB_CODIMP" , "Código do Impacto"             , "string", "Impacto"       , "TAB_CODIMP" )
	self:addProperty( "TAE_DESCRI" , "Descrição do Impacto"          , "string", "Desc. Impacto" , "TAE_DESCRI" )
	self:addProperty( "TAB_CODCLA" , "Código da Classe"              , "string", "Classe"        , "TAB_CODCLA" )
	self:addProperty( "TA8_DESCRI" , "Descrição da Classe"           , "string", "Desc. Classe"  , "TA8_DESCRI" )

return self:oSchema

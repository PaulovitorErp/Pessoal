#include "msobject.ch"
#include "tlpp-core.th"
#include "tlpp-rest.th"
#include "totvs.framework.treports.integratedprovider.th"

namespace totvs.protheus.SGA.AspectosPorCriticidadeSmartViewBusinessObject

//-------------------------------------------------------------------
/*/{Protheus.doc} AspectosPorCriticidadeSmartViewBusinessObject
Objeto de negocio de Aspectos por Criticidade

@author Matheus Wilbert
@since 20/11/2023
@version P11
@param param_name, param_type, param_descr
@return Nil
/*/
//-------------------------------------------------------------------
@totvsFrameworkTReportsIntegratedProvider( active = .T., team = "SIGASGA", tables = "TAB, TA8, TA4, TAE", name = "Aspecto por Criticidade", country = "ALL", initialRelease = "12.1.2210" )
class AspectosPorCriticidadeSmartViewBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider    

    Public Method new() as object
    Public Method getAreas() as array
    Public Method getDescription() as character
    Public Method getData() as object
    Public Method getSchema() as object

EndClass

//-------------------------------------------------------------------
/*/{Protheus.doc} New
Metodo Construtor do Objeto de NegÃ³cio
@type  Metodo

@author Matheus Wilbert
@since 20/11/2023
@version P11
@return Nil
/*/
//-------------------------------------------------------------------    
Method New() as object class AspectosPorCriticidadeSmartViewBusinessObject
    _Super:new()
    self:setDisplayName( "Aspectos por Criticidade" )
Return self

//-------------------------------------------------------------------
/*/{Protheus.doc} getDescription
Descricao do Objeto de Negocio
@type Metodo

@author Matheus Wilbert
@since 20/11/2023
@version P11
@return caractere, Conteudo da descricao do Objeto de Negocio
/*/
//-------------------------------------------------------------------
Method getDescription() as character class AspectosPorCriticidadeSmartViewBusinessObject
Return "Relatório dos Aspectos por Criticidade"

//-------------------------------------------------------------------
/*/{Protheus.doc} getAreas
Areas do menu que o Objeto de Negocio deve ficar   
@type Metodo

@author Matheus Wibert
@since 20/11/2023
@version P11
@return array, Retorna um array contendo as sessÃµes do Menu em que o 
Objeto de Negocio deve aparecer
/*/
//-------------------------------------------------------------------
Method getAreas() as array class AspectosPorCriticidadeSmartViewBusinessObject
Return { "SGA" }

//-------------------------------------------------------------------
/*/{Protheus.doc} GetData
Metodo responsavel pelo processamento dos dados enviados pelo 
Objeto de Negocio
@type Metodo

@author Matheus Wilbert
@since 20/11/2023
@version P11
@param nPage, numerico, numero da pagina atual
@param oFilter, objeto, Filtros aplicados pelo usuÃ¡rio
@return objeto, Retorna o proprio objeto
/*/
//-------------------------------------------------------------------
Method getData( nPage as numeric, oFilter as object ) as object class AspectosPorCriticidadeSmartViewBusinessObject

    Local cAliasTAB      as character
    Local cTAB           as character 
    Local cTA8           as character
    Local cTA4           as character
    Local cTAE           as character

    Local jParams   as json

    Local nSkip     as numeric
    Local nCount    as numeric

    nCount  := 0
    jParams := oFilter:getParameters() // metodo para retorno do json dos parametros

    
    cAliasTAB   := GetNextAlias()
    cTAB        := xFilial( 'TAB' )
    cTA8        := xFilial( 'TA8' )
    cTA4        := xFilial( 'TA4' )
    cTAE        := xFilial( 'TAE' )

    BeginSQL Alias cAliasTAB
        SELECT TAB.TAB_CODCLA, TAB.TAB_ORDEM, TAB.TAB_CODASP, TAB.TAB_CODIMP, TAB.TAB_RESULT, TAB.TAB_DTRESU,
            TA8.TA8_DESCRI, TA4.TA4_DESCRI, TAE.TAE_DESCRI
        FROM %table:TAB% TAB
        LEFT JOIN %table:TA8% TA8 ON
            TAB.TAB_CODCLA = TA8.TA8_CODCLA
            AND %exp:cTA8% = TA8.TA8_FILIAL
            AND TAB.%notDel%
        LEFT JOIN %table:TAE% TAE ON
            TAB.TAB_CODIMP = TAE.TAE_CODIMP
            AND %exp:cTAE% = TAE.TAE_FILIAL
            AND TAB.%notDel%
        LEFT JOIN %table:TA4% TA4 ON
            TAB.TAB_CODASP = TA4.TA4_CODASP
            AND %exp:cTA4% = TA4.TA4_FILIAL
            AND TAB.%notDel%
        WHERE 
            TAB.TAB_FILIAL = %exp:cTAB% 
            AND TAB.TAB_CODASP BETWEEN %exp:jParams[ 'MV_PAR01', 1 ]% AND %exp:jParams[ 'MV_PAR02', 1]%
            AND TAB.TAB_CODCLA BETWEEN %exp:jParams[ 'MV_PAR05', 1 ]% AND %exp:jParams[ 'MV_PAR06', 1]%
            AND TAB.TAB_DTRESU BETWEEN %exp:substr(ArrTokStr(jParams['MV_PAR03']), 1, 4) + substr(ArrTokStr(jParams['MV_PAR03']), 6, 2) + substr(ArrTokStr(jParams['MV_PAR03']), 9, 2)% AND %exp: substr(ArrTokStr(jParams['MV_PAR04']), 1, 4) + substr(ArrTokStr(jParams['MV_PAR04']), 6, 2) + substr(ArrTokStr(jParams['MV_PAR04']), 9, 2)%
            AND TAB.%notDel%
    EndSQL

    dbSelectArea( cAliasTAB )
	dbGoTop()

    self:setPageSize( 15 )

    // Paginacao
    If nPage == 1

        ( cAliasTAB )->( dbGoTop() )

    EndIf

    While !( cAliasTAB )->( Eof() )
        self:oData:appendData( { "TAB_CODCLA": ( cAliasTAB )->TAB_CODCLA, ;
                "TAB_ORDEM":  ( cAliasTAB )->TAB_ORDEM , ;
                "TAB_CODASP": ( cAliasTAB )->TAB_CODASP, ;
                "TAB_CODIMP": ( cAliasTAB )->TAB_CODIMP, ;
                "TAB_RESULT": ( cAliasTAB )->TAB_RESULT, ;
                "TAB_DTRESU": totvs.framework.treports.date.stringToTimeStamp( ( cAliasTAB )->TAB_DTRESU ), ;
                "TA8_DESCRI": ( cAliasTAB )->TA8_DESCRI, ;
                "TA4_DESCRI": ( cAliasTAB )->TA4_DESCRI, ;
                "TAE_DESCRI": ( cAliasTAB )->TAE_DESCRI } )
    ( cAliasTAB )->( dbSkip() )
 
    Enddo
 
    self:setHasNext( ( cAliasTAB )->( !Eof() ) )
 
    ( cAliasTAB )->( DBCloseArea() )
    
Return self:oData

//-------------------------------------------------------------------
/*/{Protheus.doc} getSchema
Metodo responsavel por informar as caracteristicas dos campos e 
tambem os parametros quando esses nao sao adicionados pela SX1
@type Metodo

@author Matheus Wilbert
@since 20/11/2023
@version P11

@return objeto, Retorna a si mesmo
/*/
//-------------------------------------------------------------------
Method getSchema() as object class AspectosPorCriticidadeSmartViewBusinessObject

    Self:AddParameter( 'MV_PAR01', 'De Aspecto?'                , 'string'  , .F. )
    Self:AddParameter( 'MV_PAR02', 'Ate Aspecto?'               , 'string'  , .F. )
    Self:AddParameter( 'MV_PAR03', 'De Data Avaliacao?'         , 'date'    , .F. )
    Self:AddParameter( 'MV_PAR04', 'Ate Data Avaliacao?'        , 'date'    , .F. )       
    Self:AddParameter( 'MV_PAR05', 'De Criticidade?'            , 'string'  , .F. )
    Self:AddParameter( 'MV_PAR06', 'Ate Criticidade?'           , 'string'  , .F. )

    Self:addProperty( 'TAB_CODCLA'  , 'Criticidade'             , 'string'      , 'Criticidade'           , 'TAB_CODCLA' )
    Self:addProperty( 'TAB_ORDEM'   , 'N. Ordem'                , 'string'      , 'N. Ordem'              , 'TAB_ORDEM'  )
    Self:addProperty( 'TAB_CODASP'  , 'Codigo do Aspecto'       , 'string'      , 'Codigo do Aspecto'     , 'TAB_CODASP' )
    Self:addProperty( 'TAB_CODIMP'  , 'Codigo do Impacto'       , 'string'      , 'Codigo do Impacto'     , 'TAB_CODIMP' )
    Self:addProperty( 'TAB_RESULT'  , 'Resultado'               , 'number'      , 'Resultado'             , 'TAB_RESULT' )
    Self:addProperty( 'TAB_DTRESU'  , 'Data do Resultado'       , 'date'        , 'Data do Resultado'     , 'TAB_DTRESU' )
    Self:addProperty( 'TA8_DESCRI'  , 'Descricao da Classe'     , 'string'      , 'Descricao da Classe'   , 'TA8_DESCRI' )
    Self:addProperty( 'TA4_DESCRI'  , 'Descricao do Aspecto'    , 'string'      , 'Descricao do Aspecto'  , 'TA4_DESCRI' )
    Self:addProperty( 'TAE_DESCRI'  , 'Descricao do Impacto'    , 'string'      , 'Descricao do Impacto'  , 'TAE_DESCRI' )

Return Self:oSchema

#Include 'tlpp-core.th'

Namespace TotvsDevelopers.Demo.Services

Using Namespace TotvsDevelopers.Demo.Adapters

Class AssetsService From NgBaseService
    Private Data _oAssetsAdapter

  	Public Method New()
	  Public Method GetPage( nCurrentPage As Numeric, nPageSize As Numeric, cFilter as Character )
    Public Method FindById( cId As Character ) As Json

EndClass

Method New() CLASS AssetsService
	_Super:New()
return

Method GetPage( nCurrentPage As Numeric, nPageSize As Numeric, cFilter as Character ) Class AssetsService

    Local jResult  := JsonObject():New()

    Default nCurrentPage := 1
    Default nPageSize    := 10
    Default cFilter      := ''

    Self:_oAssetsAdapter := NgAssetsAdapter():New()
    Self:_oAssetsAdapter:BuildPage( nCurrentPage, nPageSize, cFilter )

    If Self:_oAssetsAdapter:lOk
        jResult['success'] := .T.
        jResult['result'] := Self:_oAssetsAdapter:GetJSONResponse()
        jResult['hasNext'] := .F.
    EndIf

Return jResult

Method FindById( cId As Character ) As Json Class AssetsService

    Local aArea    := FWGetArea()
    Local aAreaST9 := ST9->( FWGetArea() )
    Local jResult  := JsonObject():New()

    jResult['result'] := JsonObject():New()

    // Tratativa para quando o usuário escolher de branco a ZZZZZ
    If 'ZZ' $ Upper( cId )
      jResult['success'] := .T.
      jResult['result']['id'] := AllTrim( cId )
    Else
      DbSelectArea( 'ST9' )
      DbSetOrder( 1 )
      If ( MsSeek( FWxFilial( 'ST9' ) + cId ) )
          jResult['success'] := .T.
          jResult['result']['id'] := ST9->T9_CODBEM
          jResult['result']['description'] := AllTrim( ST9->T9_NOME )
      Else
          jResult['success'] := .F.
          jResult['result']['code'] := 404
          jResult['result']['message'] := "Bem não encontrado"
          jResult['result']['detailedMessage'] := "O bem " + cId + " informado não existe!"
      EndIf
    EndIf

    FWRestArea( aAreaST9 )
    FWRestArea( aArea )

Return jResult

#Include 'tlpp-core.th'

Namespace TotvsDevelopers.Demo.Services

Using Namespace TotvsDevelopers.Demo.Adapters

Class costCenterService From NgBaseService
    Private Data _ocostCenterAdapter

  	Public Method New()
	  Public Method GetPage( nCurrentPage As Numeric, nPageSize As Numeric, cFilter as Character  )
    Public Method FindById( cId As Character ) As Json

EndClass

Method New() CLASS costCenterService
	_Super:New()
return

Method GetPage( nCurrentPage As Numeric, nPageSize As Numeric, cFilter as Character ) Class costCenterService

    Local jResult  := JsonObject():New()

    Default nCurrentPage := 1
    Default nPageSize    := 10
    Default cFilter      := ''

    Self:_ocostCenterAdapter := costCenterAdapter():New()
    Self:_ocostCenterAdapter:BuildPage( nCurrentPage, nPageSize, cFilter )

    If Self:_ocostCenterAdapter:lOk
        jResult['success'] := .T.
        jResult['result']  := Self:_ocostCenterAdapter:GetJSONResponse()
        jResult['hasNext'] := .F.
    EndIf

Return jResult

Method FindById( cId As Character ) As Json Class costCenterService

    Local aArea    := FWGetArea()
    Local aAreaCTT := CTT->( FWGetArea() )
    Local jResult  := JsonObject():New()

    jResult['result'] := JsonObject():New()

    If 'ZZ' $ Upper( cId )
      jResult['success'] := .T.
      jResult['result']['id'] := AllTrim( cId )
    Else
      DbSelectArea( 'CTT' )
      DbSetOrder( 1 )

      If ( MsSeek( FWXFilial( 'CTT' ) + cId ) )
          jResult['success'] := .T.
          jResult['result']['id'] := CTT->CTT_CUSTO
          jResult['result']['description'] := AllTrim( CTT->CTT_DESC01 )
      Else
          jResult['success'] := .F.
          jResult['result']['code'] := 404
          jResult['result']['message'] := "Centro de Custo não encontrado!"
          jResult['result']['detailedMessage'] := "O Centro de Custo " + cId + " não foi encontrado!"
      EndIf
    EndIf

    FWRestArea( aAreaCTT )
    FWRestArea( aArea )

Return jResult

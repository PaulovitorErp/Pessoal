#Include 'tlpp-core.th'

Namespace TotvsDevelopers.Demo.Adapters

Class NgAssetsAdapter From FWAdapterBaseV2

  Private Data _cQuery As Character

  Public Method New()
  Public Method BuildPage( nCurrentPage As Numeric, nPageSize As Numeric, cFilter as Character )
  Private Method _ConfigJsonResultSet()

EndClass

Method New() CLASS NgAssetsAdapter
    _Super:New( 'GET', .T. )

    Self:_ConfigJsonResultSet()
return

Method BuildPage( nCurrentPage As Numeric, nPageSize As Numeric, cFilter as Character ) Class NgAssetsAdapter

    Local cWhere := "T9_FILIAL = " + ValToSQL( FWxFilial( 'ST9' ) )

    Default nCurrentPage := 1
    Default nPageSize    := 10
    Default cFIlter      := ''

    // Caso seja feita pesquisa por um registro especifico, adiciona na query o valor recebido por parametro do front.
    If !Empty( cFilter )
      cWhere += " AND T9_CODBEM like '%" + cFilter + "%'"
    EndIf

    Self:SetPage( nCurrentPage )
    Self:SetPageSize( nPageSize )
    Self:SetQuery( Self:_cQuery )
    Self:SetWhere( cWhere + " AND D_E_L_E_T_ = ' '" )
    Self:SetOrder( "T9_FILIAL, T9_CODBEM" )

    If Self:Execute()
      Self:FillGetResponse()
    EndIf

Return

Method _ConfigJsonResultSet() Class NgAssetsAdapter

	Self:_cQuery := "SELECT #QueryFields# FROM " + RetSqlName( 'ST9' )
 	Self:_cQuery += " WHERE #QueryWhere# "

 	Self:AddMapFields( "id", "T9_CODBEM"  , .T., .T., { "T9_CODBEM", "C", TamSX3( "T9_CODBEM" )[1], 0 } )
  Self:AddMapFields( "description", "T9_NOME" , .T., .F., { "T9_NOME", "C", TamSX3( "T9_NOME" )[1], 0 } )

Return
